apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 'android-O'
    buildToolsVersion "26.0.0-rc2"

    defaultConfig {
        applicationId "fmrsabino.moviesdb"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "MOVIES_DB_API_KEY", getKey("MOVIES_DB_API_KEY")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        //https://github.com/ReactiveX/RxJava/issues/4445
        exclude 'META-INF/rxjava.properties'
    }
}

static def getKey(String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("project_keys"))
    return properties.getProperty(property)
}

dependencies {
    final SUPPORT_LIB_VERSION = '26.0.0-beta1'

    final MOSHI_VERSION = '1.4.0'
    final TIMBER_VERSION = '4.5.1'
    final RETROFIT_VERSION = '2.3.0'
    final RX_ANDROID_VERSION = '2.0.1'
    final RX_JAVA_VERSION = '2.1.0'
    final RX_KOTLIN_VERSION = '2.0.3'
    final DAGGER_VERSION = '2.10'
    final RX_BINDING_VERSION = '2.0.0'
    final MOCKITO_VERSION = '2.8.9'
    final ESPRESSO_VERSION = '2.2.2'
    final ARCH_COMPONENTS_VERSION = '1.0.0-alpha1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$SUPPORT_LIB_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIB_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIB_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIB_VERSION"

    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"

    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$RX_BINDING_VERSION"
    implementation "com.squareup.moshi:moshi:$MOSHI_VERSION"
    //implementation "com.squareup.moshi:moshi-kotlin:$MOSHI_VERSION" https://github.com/square/moshi/issues/309
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-moshi:$RETROFIT_VERSION"
    implementation "com.github.akarnokd:rxjava2-interop:0.10.0"

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA_VERSION"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID_VERSION"
    implementation "io.reactivex.rxjava2:rxkotlin:$RX_KOTLIN_VERSION"

    //Dependency Injection
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    //Android Architecture Components
    implementation "android.arch.lifecycle:runtime:$ARCH_COMPONENTS_VERSION"
    implementation "android.arch.lifecycle:extensions:$ARCH_COMPONENTS_VERSION"
    kapt "android.arch.lifecycle:compiler:$ARCH_COMPONENTS_VERSION"
    implementation "android.arch.persistence.room:runtime:$ARCH_COMPONENTS_VERSION"
    kapt "android.arch.persistence.room:compiler:$ARCH_COMPONENTS_VERSION"
    implementation "android.arch.persistence.room:rxjava2:$ARCH_COMPONENTS_VERSION"

    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    //Unit
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:$MOCKITO_VERSION"
    testImplementation "org.robolectric:robolectric:3.3.2"

    //Instrumentation
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "org.mockito:mockito-core:$MOCKITO_VERSION"
    androidTestImplementation "com.android.support:support-annotations:$SUPPORT_LIB_VERSION"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    androidTestImplementation('com.android.support.test:runner:0.5')
}
repositories {
    mavenCentral()
}
