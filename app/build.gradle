apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 'android-O'
    buildToolsVersion "26.0.0-rc2"

    defaultConfig {
        applicationId "fmrsabino.moviesdb"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "MOVIES_DB_API_KEY", getKey("MOVIES_DB_API_KEY")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        //https://github.com/ReactiveX/RxJava/issues/4445
        exclude 'META-INF/rxjava.properties'
    }
}

def getKey(String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("project_keys"))
    return properties.getProperty(property)
}

kapt {
    generateStubs = true
}

dependencies {
    final SUPPORT_LIB_VERSION = '26.0.0-beta1'

    final MOSHI_VERSION = '1.4.0'
    final TIMBER_VERSION = '4.5.1'
    final RETROFIT_VERSION = '2.3.0'
    final RX_ANDROID_VERSION = '2.0.1'
    final RX_JAVA_VERSION = '2.1.0'
    final RX_KOTLIN_VERSION = '2.0.3'
    final DAGGER_VERSION = '2.10'
    final RX_BINDING_VERSION = '2.0.0'
    final MOCKITO_VERSION = '2.8.9'
    final ESPRESSO_VERSION = '2.2.2'
    final ARCH_COMPONENTS_VERSION = '1.0.0-alpha1'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:appcompat-v7:$SUPPORT_LIB_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIB_VERSION"
    compile "com.android.support:design:$SUPPORT_LIB_VERSION"

    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"

    compile "com.jakewharton.rxbinding2:rxbinding-kotlin:$RX_BINDING_VERSION"
    compile "com.squareup.moshi:moshi:$MOSHI_VERSION"
    //compile "com.squareup.moshi:moshi-kotlin:$MOSHI_VERSION" https://github.com/square/moshi/issues/309
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    //Retrofit
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-moshi:$RETROFIT_VERSION"
    compile "com.github.akarnokd:rxjava2-interop:0.10.0"

    //Rx
    compile "io.reactivex.rxjava2:rxjava:$RX_JAVA_VERSION"
    compile "io.reactivex.rxjava2:rxandroid:$RX_ANDROID_VERSION"
    compile "io.reactivex.rxjava2:rxkotlin:$RX_KOTLIN_VERSION"

    //Dependency Injection
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt    "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    //Android Architecture Components
    compile "android.arch.lifecycle:runtime:$ARCH_COMPONENTS_VERSION"
    compile "android.arch.lifecycle:extensions:$ARCH_COMPONENTS_VERSION"
    kapt    "android.arch.lifecycle:compiler:$ARCH_COMPONENTS_VERSION"

    compile 'com.squareup.sqlbrite:sqlbrite:1.1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    //Unit
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    testCompile "org.robolectric:robolectric:3.3.2"

    //Instrumentation
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIB_VERSION"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    androidTestCompile ('com.android.support.test:runner:0.5')
}
repositories {
    mavenCentral()
}
